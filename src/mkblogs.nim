import std/[algorithm, logging, os, strutils, sugar, tables, times]
from std/posix import errno, strerror

import pkg/[jsony, mummy, mummy/routers, markdown, regex, colored_logger]
import ./[argparser]

when defined(linux):
  var PR_SET_NO_NEW_PRIVS {.importc, header: "<linux/prctl.h>".}: int32
  proc prctl(
    opt: int32, arg2, arg3, arg4, arg5: uint64
  ): int32 {.importc, header: "<sys/prctl.h>".}

type
  Index* = object
    flap*: string
    contacts*: Table[string, string]

  OpenGraphData* = object
    title*: string
    description*: string
    url*: string

  Meta* = object
    title*: string
    port*: uint16 = 8080
    index*: Index
    opengraph*: OpenGraphData

  Article* = object
    path*: string
    creationDate*: Time
    title*: string

const
  TagsExpr = re2("<[^>]+>")
  TimeFormatVerbose = "HH:mm yyyy-MM-dd"
  TimeFormatDisplay = "MMMM d, yyyy"

func removeHtmlTags(str: string): string =
  replace(str, TagsExpr, "")

func getOpengraphTags(
    title: string, description: string, url: string, kind: string = "website"
): string =
  let
    title =
      if title.len < 1: "No OpenGraph title was specified for this website." else: title

    description =
      if description.len < 1:
        "If you are the owner of this site, go to `meta.json` and add the appropriate `opengraph` entries."
      else:
        description

  """
<meta property="og:title" content="$1">
<meta property="og:description" content="$2">
<meta property="og:url" content="$3">
<meta property="og:type" content="$4">
  """ %
    [title, description, url, kind]

proc attachRouterPaths(router: var Router, dir: string, meta: Meta): seq[Article] =
  # Add a route for the favicon
  let favicon = readFile(dir / "favicon.png")
  router.get(
    "/favicon.png",
    proc(request: Request) {.gcsafe.} =
      var headers: HttpHeaders
      headers["Content-Type"] = "image/png"
      request.respond(200, headers = ensureMove(headers), body = favicon),
  )

  var articles: seq[Article]
  for path in walkDirRec(dir / "entries"):
    var pRouter = router.addr
    capture path:
      let
        creationTimeObj = getCreationTime(path)
        creationTime = creationTimeObj.format(TimeFormatDisplay)

      let rendered = markdown(readFile(path))
      let renderSplitted = rendered.splitLines()
      let title =
        if renderSplitted.len > 0:
          renderSplitted[0].removeHtmlTags()
        else:
          "Untitled Entry"

      let description =
        if renderSplitted.len > 1:
          renderSplitted[1].removeHtmlTags()
        else:
          "No description can be rendered"

      let
        splittedPath = path.split('/')
        path = '/' & splittedPath[1 ..< splittedPath.len].join("/").changeFileExt("")

      let buffer =
        """
  <!DOCTYPE html>
  <html>
    <!--Document generated by mkblogs (https://github.com/xTrayambak/mkblogs)-->
    <head>
      <title>$1 - $2</title>
      <link href="https://fonts.googleapis.com/css2?family=Roboto:wght@400;700&display=swap" rel="stylesheet">
      <link rel="icon" type="image/png" href="../favicon.png">
      $3

      <style>
      h1 { font-weight: 800; font-size: 6.5rem; }
      h2 { font-weight: 700; font-size: 3.5rem; }
      h3, h4, h5, h6 { font-weight: 600; font-size: 2rem; }

      body
      {
        background-color: #3c3836;
        overflow-x: hidden;
      }

      img
      {
        display: block;
        margin-left: auto;
        margin-right: auto;
      }

      footer
      {
        position: absolute;
        bottom: 0;
        width: 100%;
        text-align: center;
        padding: 1em;
      }

      h1, h2, h3, h4, h5, h6, p, footer, li
      {
        color: #fff;
      }

      * { font-family: 'Roboto', sans-serif; }

      a
      {
        color: lightgray;
        text-decoration: none;
      }

      a:hover { color: white; }
      </style>
    </head>
    <body>
      <section>
        $4
      </section>
      <footer>Created on <strong>$5</strong></footer>

      <!-- Code highlighting stuff -->
      <link href="https://cdn.jsdelivr.net/npm/prismjs@1.30.0/themes/prism-tomorrow.css" rel="stylesheet" />
      <script src="https://cdn.jsdelivr.net/npm/prismjs@1.30.0/prism.js"></script>
      <script src="https://cdn.jsdelivr.net/npm/prismjs@1.30.0/components/prism-nim.min.js"></script>
    </body>
  </html>
      """ %
        [
          title,
          meta.title,
          getOpengraphTags(
            title, description, meta.opengraph.url & '/' & path, "article"
          ),
          rendered,
          creationTime,
        ]

      proc viewer(request: Request) {.gcsafe.} =
        var headers: HttpHeaders
        headers["Content-Type"] = "text/html; charset=UTF-8"
        headers["Server"] = "mkblogs/mummy"
        request.respond(200, headers = ensureMove(headers), body = buffer)

      capture path:
        let
          splittedPath = path.split('/')
          path = '/' & splittedPath[1 ..< splittedPath.len].join("/").changeFileExt("")
        articles &= Article(title: title, path: path, creationDate: creationTimeObj)
        pRouter[].get(path, viewer)

  move(articles)

proc attachIndex(router: var Router, meta: Meta, articles: seq[Article]) =
  var renderedEntries = newStringOfCap(4096)
  var renderedContacts = newStringOfCap(1024)
  for article in articles.sortedByIt(it.creationDate).reversed():
    renderedEntries &=
      """
      <div class="entry">
        <a href="$1">
          <h3>$2</h3>
        </a>
        <h6 style="margin-top: auto;"><strong>$3</strong></h6>
      </div>
    """ %
      [article.path, article.title, article.creationDate.format(TimeFormatDisplay)]

  for contactMeth, contactInfo in meta.index.contacts:
    renderedContacts &=
      """
        <div class="contact-info">
          <h4><strong>$1</strong> â€” $2</h4>
        </div>
      """ %
      [contactMeth, contactInfo]

  let buffer =
    """
<!DOCTYPE html>
<html>
  <!--Index page generated by mkblogs (https://github.com/xTrayambak/mkblogs)-->
  <head>
    <title>$1</title>
    <link href="https://fonts.googleapis.com/css2?family=Roboto:wght@400;700&display=swap" rel="stylesheet">
    <link rel="icon" type="image/png" href="favicon.png">
    $2
    <style>
      *
      {
        box-sizing: border-box;
        margin: 0;
        padding: 0;
        font-family: 'Roboto', sans-serif;
      }

      body, html
      {
        margin: 0;
        padding: 0;
        height: 100%;
        background-color: #282828;
      }

      .main
      {
        display: flex;
        height: 100vh;
      }

      .entries
      {
        flex: 0 0 80%;
        padding: 1rem;
        overflow-y: auto;
        background-color: #3c3836;
      }

      .flap
      {
        flex: 0 0 20%;
        height: 100vh;
        overflow-y: auto;
        overflow-x: hidden;
        padding: 2rem;
        background-color: #32302f;
        color: #fff;
        box-sizing: border-box;
        overflow-y: auto;
      }

      .entry
      {
        margin-bottom: 1.5rem;
        padding: 1rem;
        background-color: #32302f;
        border-radius: 1rem;
        display: flex;
        flex-direction: column;
        border-radius: 1rem;
        color: #fff;
      }

      .contact-info
      {
        margin-top: 0.475rem;
        margin-bottom: 0.475rem;
      }

      .flap > .title
      {
        margin-bottom: 1rem;
      }

      .flap > .text
      {
        margin-bottom: 1.5rem;
      }

      .flap > .contact-me
      {
        margin-bottom: 1rem;
      }

      a
      {
        color: lightgray;
        text-decoration: none;
      }

      a:hover { color: white; }
    </style>
  </head>
  <body>
    <div class="main">
      <div class="entries">
        $3
      </div>
      <div class="flap">
        <h2 class="title">$1</h2>
        <p class="text">$4</p>
        <h3 class="contact-me">Contact Me</h3>
        $5
      </div>
    </div>
  </body>
</html>
    """ %
    [
      meta.title,
      getOpengraphTags(
        meta.opengraph.title, meta.opengraph.description, meta.opengraph.url
      ),
      ensureMove(renderedEntries),
      meta.index.flap,
      ensureMove(renderedContacts),
    ]

  proc viewer(request: Request) {.gcsafe.} =
    var headers: HttpHeaders
    headers["Content-Type"] = "text/html; charset=UTF-8"
    headers["Server"] = "mkblogs via mummy backend"

    request.respond(200, headers = ensureMove(headers), body = buffer)

  router.get("/", viewer)

proc serveProject(dir: string) =
  info "mkblogs: constructing project"

  debug "mkblogs: reading project's meta.json"
  let meta = readFile(dir / "meta.json").fromJson(Meta)

  debug "mkblogs: constructing blog paths and articles"
  var router: Router
  let articles = attachRouterPaths(router, dir, meta)

  debug "mkblogs: done; attaching index path and page"
  attachIndex(router, meta, articles)

  debug "mkblogs: creating HTTP server"
  var server = newServer(
    ensureMove(router),
    logHandler = proc(level: common.LogLevel, args: varargs[string]) {.gcsafe.} =
      const label = "http: "
      var size = label.len
      for arg in args:
        size += arg.len + 1

      dec size

      var msg = newStringOfCap(ensureMove(size))
      msg &= label
      for i, arg in args:
        msg &= arg
        if i < args.len - 1:
          msg &= ' '

      case level
      of DebugLevel:
        debug ensureMove(msg)
      of InfoLevel:
        info ensureMove(msg)
      of ErrorLevel:
        error ensureMove(msg)
    ,
  )

  info "mkblogs: now serving a project on port " & $meta.port
  server.serve(Port(meta.port), "0.0.0.0")

proc installSandbox() =
  when defined(linux):
    debug "sandbox: enabling NoNewPrivs"
    if prctl(PR_SET_NO_NEW_PRIVS, 1, 0, 0, 0) != 0:
      error "sandbox: failed to enable NoNewPrivs: " & $strerror(errno)
  else:
    warn "sandbox: mkblogs does not attempt to harden itself on your platform. Just keep that in mind."

proc main() {.inline.} =
  addHandler(newColoredLogger())
  setLogFilter(lvlInfo)

  var input = parseInput()
  if input.enabled("verbose", "v"):
    setLogFilter(lvlAll)

  case input.command
  of "serve":
    let dir = input.arguments[0]
    installSandbox()
    serveProject(dir)
  else:
    error "mkblogs: invalid mode: " & input.command
    quit(1)

when isMainModule:
  main()
